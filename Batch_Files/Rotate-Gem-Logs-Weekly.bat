
	@echo off
	cls
	setlocal ENABLEDELAYEDEXPANSION

::========================================================================================================================================
:: Title:    Rotate GEMonline & GEMDaily Logs -- Weekly
::========================================================================================================================================
::
::	Author:	Roger Corley
::	Created:	May 14, 2015  11:39:53 AM
::............................................................
::
::	Version: 	1.40
::	Updated:	Roger Corley - July 14, 2015  11:23 AM
::
::========================================================================================================================================
:: Description:
::========================================================================================================================================
::
::	A log rotation batch file to be run on a weekly basis. This batch will rotate and archive
::	the 'gemonline.log' and 'GEMDaily.cp' log files stored on the system drive (C:\). Without
::	an archival process, these files continue to grow, which will eventually begin to slow any
::	transactions between Micros and GEMpay.
::
::
::---------------------------------------------------------------------------------------------------------------------------------------
:: ---[ Archives]---
::---------------------------------------------------------------------------------------------------------------------------------------
::
::	These logs are archived to a directory located on the C:\ drive named '_Gem-Log-Archives'.
::	The archive logs will be renamed and timestamped with the following nomenclature:
::
::.......................................................................................................................................
::
::	'gemonline' = 'C:\_Gem-Log-Archives\gemonline.<datetime>.log0<#>'
::------------------------------------------------------------------------
::	'GEMDaily.cp' = 'C:\_Gem-Log-Archives\GEMDaily.<datetime>.cp0<#>'
::
::.......................................................................................................................................
::
::	Each archived log will be stored for a maximum of twelve (12) weeks before being deleted
::	through the rotation process.
::
::
::---------------------------------------------------------------------------------------------------------------------------------------
::  ---[ Archive Location ]---
::---------------------------------------------------------------------------------------------------------------------------------------
::
::	If the directory 'C:\_Gem-Toolbox' with a subdirectory 'Batch-Files' doesn't exist, create
::	these directories first. Save this batch file as:
::
::.......................................................................................................................................
::		'C:\_Gem-Toolbox\Batch-Files\Rotate-Gem-Logs-Weekly.bat'
::.......................................................................................................................................
::
::
::---------------------------------------------------------------------------------------------------------------------------------------
:: ---[ Scheduled Task ]---
::---------------------------------------------------------------------------------------------------------------------------------------
::
::	Create a Scheduled Task named 'Rotate-Gem-Logs-Weekly' with the following properties:
::.......................................................................................................................................
::
::	[ GENERAL ]	Run whether user is logged on or not: 'selected'
::-------------------------------------------------------------------------------------------------
::			Run with highest privileges:    'checked'
::
::.......................................................................................................................................
::
::	[ TRIGGERS ]	Settings: Weekly -- Start:  (Following Monday) at 4:45 AM
::-------------------------------------------------------------------------------------------------
::			Recur every:  '1' weeks on 'Monday' -- 'checked'
::-------------------------------------------------------------------------------------------------
::			Adv Settings: Stop task if it runs longer than: '30 mins'
::-------------------------------------------------------------------------------------------------
::			Enabled:  'checked'
::
::.......................................................................................................................................
::
::	[ ACTIONS ]	Start A Program: 'C:\_Gem-Toolbox\Batch-Files\Rotate-Gem-Files-Weekly.bat'
::
::.......................................................................................................................................
::
::	[ SETTINGS ]	Allow task to be run on demand:   'checked'
::-------------------------------------------------------------------------------------------------
::			Stop the task if it runs longer than: '1 hour'
::
::.......................................................................................................................................
::
::
::
::
::========================================================================================================================================
:: DIRECTORY: Gem-Log-Archives Variable
::========================================================================================================================================
::
::-------------------------------------------------------------------------------------
:: ---[ **NOTE** ]---	'sysD' MUST match the Drive Letter hosting the GEM directory
::			ALWAYS include the colon ( : ) after the drive letter!  e.g. D:
::-------------------------------------------------------------------------------------
::
	set "sysD=C:"

	set "archD=%sysD%\_Gem-Log-Archives"
::
::
::
::
::++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
::++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
::========================================================================================================================================
::
::			----[[[ ****  DO NOT MODIFY THIS SCRIPT   ****  ]]]----
::
::========================================================================================================================================
::++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
::++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
::
::
::
::
::========================================================================================================================================
:: EXTENSION: GEMonline
::========================================================================================================================================
::
::-------------------------------------------------------------------------------------
:: ---[ **NOTE** ]---	'gotype' MUST match the GEMonline log file extension in C:\
::			ALWAYS include the dot ( . ) before the type!
::-------------------------------------------------------------------------------------
::
	set "gotype=log"

::========================================================================================================================================
:: EXTENSION: GEMDaily
::========================================================================================================================================
::
::---------------------------------------------------------------------------------------
:: ---[ **NOTE** ]---	'gdtype' MUST match the GEMDaily log file extension in C:\
::			ALWAYS include the dot ( . ) before the type!
::---------------------------------------------------------------------------------------
::
	set "gdtype=cp"

::========================================================================================================================================
:: DATE CONSTANTS
::========================================================================================================================================
::
	for /f "delims=" %%a in ('wmic OS Get localdatetime  ^| find "."') do set "dt=%%a"

	set "YYYY=%dt:~0,4%"
	set "MM=%dt:~4,2%"
	set "DD=%dt:~6,2%"
	set "HH=%dt:~8,2%"
	set "Min=%dt:~10,2%"
	set "Sec=%dt:~12,2%"

	set "datestamp=%YYYY%/%MM%/%DD%"
	set "timestamp=%HH%:%Min%:%Sec%"
	set "fullstamp=%datestamp% %timestamp%"
	set "logstamp=%YYYY%-%MM%-%DD%"

::========================================================================================================================================
:: FILES: GEMonline
::========================================================================================================================================
::
	set "goname=gemonline"
	set "gofile=%goname%.%gotype%"
	set "gopath=C:\%gofile%"

::========================================================================================================================================
::FILES: GEMDaily
::========================================================================================================================================
::
	set "gdname=GEMDaily"
	set "gdfile=%gdname%.%gdtype%"
	set "gdpath=C:\%gdfile%"

::========================================================================================================================================
::FILES: Archives
::========================================================================================================================================

	set "agofull=%archD%\%goname%.%logstamp%.%gotype%01"
	set "agdfull=%archD%\%gdname%.%logstamp%.%gdtype%01"

	set "ago=%archD%\gemonline.*.%gotype%"
	set "agd=%archD%\GEMDaily.*.%gdtype%"

::========================================================================================================================================
::FILES: Import Log File
::========================================================================================================================================
::
	set "log=%archD%\_Rotate-Gem-Logs-Weekly.log"


::========================================================================================================================================
::BEGIN BATCH ROTATIONS
::========================================================================================================================================
::
::
	goto FileRotations

::
::
::---------------------------------------------------------------------------------------------------------------------------------------
:FileRotations
::---------------------------------------------------------------------------------------------------------------------------------------
		echo. >> %log%
		echo ============================================================================== >> %log%
		echo %fullstamp% : [ ROTATE ] : GEMonline - GEMDaily Log Rotations >> %log%
		echo ============================================================================== >> %log%
		echo. >> %log%

		echo .............................................................................. >> %log%
		echo %fullstamp% : [ MSG ] : Starting Log Rotations >> %log%
		echo .............................................................................. >> %log%


	goto chkGO

::---------------------------------------------------------------------------------------------------------------------------------------
:chkGO
::---------------------------------------------------------------------------------------------------------------------------------------

	if exist %gopath% (
		echo .............................................................................. >> !log!
		echo !fullstamp! : [ CHECKFILE ] : Checking Exist : !gopath! >> !log!
		echo !fullstamp! : [ CHECKFILE ] : !gopath! found. >> !log!
		echo !fullstamp! : [ CHECKFILE ] : Starting GEMonline Log Rotations >> !log!
		echo .............................................................................. >> !log!


	goto rotateGO

	) else (
		echo ============================================================================== >> !log!
		echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> !log!
		echo !fullstamp! : [ CHECK : ERROR ] : '!gopath!' NOT FOUND >> !log!
		echo !fullstamp! : [ CHECK : ERROR ] : Moving to Check GEMDaily >> !log!
		echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> !log!
		echo ============================================================================== >> !log!
		echo. >> !log!
	)

	goto chkGD

::---------------------------------------------------------------------------------------------------------------------------------------
:rotateGO
::---------------------------------------------------------------------------------------------------------------------------------------

cd C:\_Gem-Log-Archives

	if exist gemonline.*.log12 del /f /q gemonline.*.log12
	if exist gemonline.*.log11 rename gemonline.*.log11 gemonline.*.log12
	if exist gemonline.*.log10 rename gemonline.*.log10 gemonline.*.log11
	if exist gemonline.*.log09 rename gemonline.*.log09 gemonline.*.log10
	if exist gemonline.*.log08 rename gemonline.*.log08 gemonline.*.log09
	if exist gemonline.*.log07 rename gemonline.*.log07 gemonline.*.log08
	if exist gemonline.*.log06 rename gemonline.*.log06 gemonline.*.log07
	if exist gemonline.*.log05 rename gemonline.*.log05 gemonline.*.log06
	if exist gemonline.*.log04 rename gemonline.*.log04 gemonline.*.log05
	if exist gemonline.*.log03 rename gemonline.*.log03 gemonline.*.log04
	if exist gemonline.*.log02 rename gemonline.*.log02 gemonline.*.log03
	if exist gemonline.*.log01 rename gemonline.*.log01 gemonline.*.log02

	if exist %gopath% move %gopath% %agofull%

		echo .............................................................................. >> %log%
		echo !fullstamp! : [ ROTATE ] : GEMonline Log Rotations Complete. >> %log%
		echo !fullstamp! : [ ROTATE ] : Moving to Check GEMDaily. >> %log%
		echo .............................................................................. >> %log%


	goto chkGD

::---------------------------------------------------------------------------------------------------------------------------------------
:chkGD
::---------------------------------------------------------------------------------------------------------------------------------------

	if exist %gdpath% (
		echo .............................................................................. >> !log!
		echo !fullstamp! : [ CHECKFILE ] : Checking Exist : !gdpath! >> !log!
		echo !fullstamp! : [ CHECKFILE ] : !gdpath! found. >> !log!
		echo !fullstamp! : [ CHECKFILE ] : Starting GEMDaily Log Rotations >> !log!
		echo .............................................................................. >> !log!


	goto rotateGD

	) else (
		echo ============================================================================== >> !log!
		echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> !log!
		echo !fullstamp! : [ CHECK : ERROR ] : '!gdpath!' NOT FOUND >> !log!
		echo !fullstamp! : [ CHECK : ERROR ] : Ending Log Rotation Script >> !log!
		echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> !log!
		echo ============================================================================== >> !log!
		echo. >> !log!
	)

	goto done

::---------------------------------------------------------------------------------------------------------------------------------------
:rotateGD
::---------------------------------------------------------------------------------------------------------------------------------------

	if exist GEMDaily.*.cp12 del /f /q GEMDaily.*.cp12
	if exist GEMDaily.*.cp11 rename GEMDaily.*.cp11 GEMDaily.*.cp12
	if exist GEMDaily.*.cp10 rename GEMDaily.*.cp10 GEMDaily.*.cp11
	if exist GEMDaily.*.cp09 rename GEMDaily.*.cp09 GEMDaily.*.cp10
	if exist GEMDaily.*.cp08 rename GEMDaily.*.cp08 GEMDaily.*.cp09
	if exist GEMDaily.*.cp07 rename GEMDaily.*.cp07 GEMDaily.*.cp08
	if exist GEMDaily.*.cp06 rename GEMDaily.*.cp06 GEMDaily.*.cp07
	if exist GEMDaily.*.cp05 rename GEMDaily.*.cp05 GEMDaily.*.cp06
	if exist GEMDaily.*.cp04 rename GEMDaily.*.cp04 GEMDaily.*.cp05
	if exist GEMDaily.*.cp03 rename GEMDaily.*.cp03 GEMDaily.*.cp04
	if exist GEMDaily.*.cp02 rename GEMDaily.*.cp02 GEMDaily.*.cp03
	if exist GEMDaily.*.cp01 rename GEMDaily.*.cp01 GEMDaily.*.cp02

	if exist %gdpath% move %gdpath% %agdfull%

		echo .............................................................................. >> %log%
		echo !fullstamp! : [ ROTATE ] : GEMDaily Log Rotations Complete. >> %log%
		echo !fullstamp! : [ ROTATE ] : Ending Log Rotation Script. >> %log%
		echo .............................................................................. >> %log%


	goto done

::---------------------------------------------------------------------------------------------------------------------------------------
:done
::---------------------------------------------------------------------------------------------------------------------------------------

		echo. >> %log%
		echo ============================================================================== >> %log%
		echo %fullstamp% : [ ROTATE ] : GEMonline - GEMDaily Log Rotations Completed >> %log%
		echo ============================================================================== >> %log%
		echo. >> %log%
		echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> %log%
		echo ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ >> %log%
		echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> %log%
		echo. >> %log%
