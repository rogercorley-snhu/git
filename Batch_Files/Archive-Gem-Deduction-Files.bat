	@echo off
	cls
	setlocal ENABLEDELAYEDEXPANSION

::===========================================================================================================
::
::-------[ TITLE 		]  :  BATCH SCRIPT : ARCHIVE : Payroll Deduction Files
::
::===========================================================================================================
::
::-------[ AUTHOR		] : Roger Corley
::-------[ CREATED		] : August 19, 2015 4:23:31 PM
::-------[ COPYRIGHT		] : Common CENTS Solutions - 2015
::
::-----------------------------------------------------------------------------------------------------------
::
::
::===========================================================================================================
::-------[ ENVIRONMENT VARIABLES ] : Directories
::===========================================================================================================

	SET "originD=C:\GEM\ImportExport"
	SET "targetD=C:\GEM\ImportExport\Archive\Payroll-Deduct-Archives"



::===========================================================================================================
::-------[ ****  DATE CONSTANTS : DO NOT MODIFY  ****  ]
::===========================================================================================================

	for /f "delims=" %%a in ('wmic OS Get localdatetime  ^| find "."') do set "dt=%%a"

	set "YYYY=%dt:~0,4%"
	set "MM=%dt:~4,2%"
	set "DD=%dt:~6,2%"
	set "hh=%dt:~8,2%"
	set "min=%dt:~10,2%"
	set "ss=%dt:~12,2%"

	set "stamp=_%YYYY%-%MM%-%DD%"


::===========================================================================================================
::-------[ ****  DATE CONSTANTS : DO NOT MODIFY  ****  ]
::===========================================================================================================

	SET /a cnt=0



::-------[  DIRECTORY	] : Change to Target Directory
::---------------------------------------------------------------------------------------

	CD /d %targetD%




::-------[  ROTATE SCRIPT] : Rotate Archived Files
::---------------------------------------------------------------------------------------

	for /f "skip=6 eol=: delims=" %%F in ('dir /b /o-d GEMDED12_*') do @del "%%F"





::-------[  ROTATE SCRIPT] : If Original File Exists, Copy to Archive Directory and Rename
::---------------------------------------------------------------------------------------

	if exist "%originD%\GEMDED12" copy "%originD%\GEMDED12" "%targetD%\GEMDED12%stamp%"



