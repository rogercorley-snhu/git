		@echo off
		cls
		setlocal ENABLEDELAYEDEXPANSION

::===========================================================================================================
::-------[ TITLE 		]  : BATCH SCRIPT : MASTER : Restart GEMService - Weekly
::===========================================================================================================
::
::-------[ AUTHOR		]  : Roger Corley
::-------[ CREATED		]  : June 11, 2015 11:55:31 AM
::-------[ COPYRIGHT		]  : Common CENTS Solutions - 2015
::
::-------[ VERSION		]  : 1.10
::
::-----------------------------------------------------------------------------------------------------------
::
::===========================================================================================================
::-------[ ****  DATE CONSTANTS : DO NOT MODIFY  ****  ]
::===========================================================================================================

		for /f "delims=" %%a in ('wmic OS Get localdatetime  ^| find "."') do set "dt=%%a"

		set "YYYY=%dt:~0,4%"
		set "MM=%dt:~4,2%"
		set "DD=%dt:~6,2%"
		set "HH=%dt:~8,2%"
		set "Min=%dt:~10,2%"
		set "Sec=%dt:~12,2%"

		set "datestamp=%YYYY%/%MM%/%DD%"
		set "timestamp=%HH%:%Min%:%Sec%"
		set "fullstamp=%datestamp% %timestamp%"
		set "logstamp=%YYYY%-%MM%-%DD%"


::===========================================================================================================
::-------[ CONFIGURE VARIABLES ] : ENVIRONMENT : Archive Directory Drive
::===========================================================================================================

::		** Set variable to drive letter hosting GEM Directory
::		** ( e.g. If D:\GEM, then set archD to D: )
::		------------------------------------------------------
		set "archD=C:"


::===========================================================================================================
::-------[ CONFIGURE VARIABLES ] : ENVIRONMENT : Service Name
::===========================================================================================================

		set "gs=GEMService"


::===========================================================================================================
::-------[ CONFIGURE VARIABLES ] : ENVIRONMENT : Script Log File
::===========================================================================================================

		set "log=%archD%\_Gem-Log-Archives\_Restart-GEMService.log"


::-----------------------------------------------------------------------------------------------------------
::@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
::
::-------[ ****  DO NOT MODIFY ANYTHING PAST THIS SECTION  ****  ]
::
::@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
::-----------------------------------------------------------------------------------------------------------
::
::
::===========================================================================================================
::-------[  BEGIN BATCH TASKS
::===========================================================================================================
::
::

		echo ============================================================================== >> %log%
			echo %fullstamp% : [ BEGIN ] Checking Current Status of "%gs%" >> %log%
		echo ============================================================================== >> %log%
		echo. >> %log%
		echo .............................................................................. >> %log%
		echo .............................................................................. >> %log%
		echo. >> %log%


		for /F "tokens=3 delims=: " %%H in ('sc query "!gs!" ^| findstr "        STATE"') do (
  			if /I "%%H" NEQ "RUNNING" (

				echo ============================================================================== >> !log!
				echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> !log!
					echo !fullstamp! : [ STATUS] : "!gs!" IS STOPPED. Starting Service. >> !log!
				echo ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ >> !log!
				echo ============================================================================== >> !log!
				echo. >> !log!

   				net start "!gs!"

  			) else (

				echo. >> !log!
				echo ------------------------------------------------------------------------------ >> !log!
					echo !fullstamp! : [ STATUS ] "!gs!" is running. Stopping service. >> !log!
				echo ------------------------------------------------------------------------------ >> !log!
				echo. >> !log!


				net stop "!gs!"


				echo. >> !log!
				echo ------------------------------------------------------------------------------ >> !log!
					echo !fullstamp! : [ STATUS ] Restarting service. >> !log!
				echo ------------------------------------------------------------------------------ >> !log!
				echo. >> !log!


				net start "!gs!"

			)
		)

::=========================================================================================================
:Done
::=========================================================================================================


		echo ============================================================================== >> %log%
			echo %fullstamp% : [ END ] Exiting Restart GEMService - Weekly >> %log%
		echo ============================================================================== >> %log%
		echo. >> %log%
		echo. >> %log%
		echo ****************************************************************************** >> %log%
		echo ****************************************************************************** >> %log%
		echo ****************************************************************************** >> %log%
		echo. >> %log%
		echo. >> %log%



